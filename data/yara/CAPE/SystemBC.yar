rule SystemBC_Socks
{
meta:
	description = "Identifies SystemBC RAT, Socks proxy version."
	author = "@bartblaze"
	date = "2021-07"
	tlp = "White"
        cape_type = "SystemBC Payload"

strings:
	$code1 = { 68 10 27 00 00 e8 ?? ?? ?? ?? 8d ?? 72 fe ff ff 50 68 02 02 00 00 e8 ?? ??
	?? ?? 85 c0 75 ?? c7 8? ?? ?? ?? ?? ?? ?? ?? ?? 8d ?? 60 fe ff ff 50 6a ff 68 ?? ??
	?? ?? e8 ?? ?? ?? ?? 8d ?? 60 fe ff ff 50 e8 ?? ?? ?? ?? 89 8? ?? ?? ?? ?? ff b? ??
	?? ?? ?? ff b? ?? ?? ?? ?? e8 ?? ?? ?? ?? 85 c0 75 ?? 81 b? ?? ?? ?? ?? ?? ?? ?? ??
	75 ?? c7 8? ?? ?? ?? ?? ?? ?? ?? ?? eb ?? }

	$code2 = { 55 8b ec 81 c4 d0 fe ff ff 53 57 56 8d ?? f4 2b cc 51 8d ?? ?4 10 50 e8
	?? ?? ?? ?? 6a 06 6a 01 6a 02 e8 ?? ?? ?? ?? 89 4? ?? 6a 04 ff 7? ?? 8d ?? fc 50 e8
	?? ?? ?? ?? c7 8? ?? ?? ?? ?? 01 00 00 00 6a 04 8d ?? d4 fe ff ff 50 6a 01 6a 06 ff
	7? ?? e8 ?? ?? ?? ?? 8d ?? d8 fe ff ff 50 6a ff ff 7? ?? e8 ?? ?? ?? ?? 6a 02 8d ??
	d8 fe ff ff 50 e8 ?? ?? ?? ?? 89 4? ?? 8b 4? ?? 3d 00 00 01 00 76 ?? 50 e8 ?? ?? ?? ?? }

condition:
	uint16(0) == 0x5a4d and any of them
}

rule SystemBC
{
meta:
	description = "Identifies SystemBC RAT, decrypted config."
	author = "@bartblaze"
	date = "2021-07"
	tlp = "White"
    cape_type = "SystemBC Payload"

strings:
	$ = "BEGINDATA" ascii wide fullword
	$ = "HOST1:" ascii wide fullword
	$ = "HOST2:" ascii wide fullword
	$ = "PORT1:" ascii wide fullword
	$ = "TOR:" ascii wide fullword
	$ = "-WindowStyle Hidden -ep bypass -file" ascii wide

condition:
	uint16(0) == 0x5a4d and 3 of them
}
